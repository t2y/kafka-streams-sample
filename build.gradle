plugins {
    id 'application'
    id 'java'

    id 'com.commercehub.gradle.plugin.avro' version '0.20.0'
    id 'com.github.sherter.google-java-format' version '0.9'
}

java {
    toolchain {
            languageVersion.set(JavaLanguageVersion.of(11))
    }
}

repositories {
    mavenCentral()
    jcenter()
    maven {
        url = 'https://packages.confluent.io/maven/'
    }
}

dependencies {
    // for type inference
    annotationProcessor 'org.projectlombok:lombok:1.18.16'
    compileOnly 'org.projectlombok:lombok:1.18.16'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.16'
    testCompileOnly 'org.projectlombok:lombok:1.18.16'

    // for logging
    implementation 'ch.qos.logback:logback-classic:1.2.3'
    implementation 'org.slf4j:slf4j-api:1.7.30'

    // kafka and kafka streams
    implementation 'org.apache.kafka:kafka-streams:2.6.0'
    implementation 'org.apache.kafka:kafka-clients:2.6.0'
    testImplementation 'org.apache.kafka:kafka-streams-test-utils:2.6.0'

    // kafka streams examples
    implementation 'org.apache.kafka:kafka-streams-examples:2.6.0'

    // avro
    implementation 'org.apache.avro:avro:1.10.1'
    implementation 'io.confluent:kafka-streams-avro-serde:6.0.1'
    implementation 'io.confluent:kafka-avro-serializer:6.0.1'

    // others
    implementation 'com.google.guava:guava:30.1-jre'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
}

application {
    mainClassName = 'kafka.streams.sample.stream.user.UserStreamsMain'
}

compileJava.dependsOn tasks.googleJavaFormat

test {
    useJUnitPlatform()

    if (System.getProperty('DEBUG', 'false') == 'true') {
        jvmArgs '-Xdebug', '-Xrunjdwp:transport=dt_socket,server=y,suspend=y,address=5005'
    }
}

avro {
  dateTimeLogicalType = 'JSR310'
  createOptionalGetters = true
}

tasks.withType(JavaExec) {
    jvmArgs '--add-opens', 'java.base/java.lang=ALL-UNNAMED'
    if (System.getProperty('DEBUG', 'false') == 'true') {
        jvmArgs '-Xdebug', '-Xrunjdwp:transport=dt_socket,server=y,suspend=y,address=5005'
    }
}


// streams

task runEventStreamsDSL(type: JavaExec) {
    group = 'Execution'
    description = 'Run event streams main class'
    classpath = sourceSets.main.runtimeClasspath
    main = 'kafka.streams.sample.stream.event.dsl.EventStreamsMain'
}

task runEventStreamsProcessor(type: JavaExec) {
    group = 'Execution'
    description = 'Run event streams main class'
    classpath = sourceSets.main.runtimeClasspath
    main = 'kafka.streams.sample.stream.event.processor.EventStreamsMain'
}

task runStream(type: JavaExec) {
    group = 'Execution'
    description = 'Run SampleStreams main class'
    classpath = sourceSets.main.runtimeClasspath
    main = 'kafka.streams.sample.stream.StreamsMain'
}


// producers

task runEventProducer(type: JavaExec) {
    group = 'Execution'
    description = 'Run event producer main class'
    classpath = sourceSets.main.runtimeClasspath
    main = 'kafka.streams.sample.producer.EventProducer'
}

task runOrderProducer(type: JavaExec) {
    group = 'Execution'
    description = 'Run order producer main class'
    classpath = sourceSets.main.runtimeClasspath
    main = 'kafka.streams.sample.producer.OrderProducer'
}

task runUserProducer(type: JavaExec) {
    group = 'Execution'
    description = 'Run user producer main class'
    classpath = sourceSets.main.runtimeClasspath
    main = 'kafka.streams.sample.producer.UserProducer'
}

// to be refactored

task runProducer(type: JavaExec) {
    group = 'Execution'
    description = 'Run producer main class'
    classpath = sourceSets.main.runtimeClasspath
    main = 'kafka.streams.sample.cli.ProducerMain'
}

task runConsumer(type: JavaExec) {
    group = 'Execution'
    description = 'Run consumer main class'
    classpath = sourceSets.main.runtimeClasspath
    main = 'kafka.streams.sample.cli.ConsumerMain'
}
